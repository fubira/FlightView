buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: "java"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs = ["-Xlint:deprecation"]
        options.release.set(21)
    }

    dependencies {
        implementation(libs.gson)
        implementation(libs.log4japi)
        implementation(libs.log4jcore)
    }

    repositories {
        mavenCentral()
        maven {
            name = "Minecraft Libraries"
            url = uri("https://libraries.minecraft.net")
        }
        maven {
            name = "Cloth Config"
            url = "https://maven.shedaniel.me/"
        }
    }
}

version = mod_version
group = mod_group_id
base.archivesName = "${mod_name}-${minecraft_version}"

task buildJar(type: Jar, group: "modding") {
    dependsOn ":common:build", ":fabric:build", ":neoforge:build"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from zipTree("common/build/libs/common.jar"),
        zipTree("fabric/build/libs/fabric.jar"),
        zipTree("neoforge/build/libs/neoforge.jar")

    from('./') {
        include 'LICENSE.md'
    }

    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_vendor,
                "Specification-Version"   : mod_version,
                "Implementation-Title"    : mod_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_vendor,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
assemble.finalizedBy("buildJar")
