buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: "java"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs = ["-Xlint:deprecation"]
        options.release.set(21)
    }

    dependencies {
        implementation(libs.gson)
        implementation(libs.log4japi)
        implementation(libs.log4jcore)
    }

    repositories {
        mavenCentral()
        maven {
            name = "Minecraft Libraries"
            url = uri("https://libraries.minecraft.net")
        }
        maven {
            name = "Cloth Config"
            url = "https://maven.shedaniel.me/"
        }
    }
}

group = mod_group_id
archivesBaseName = "${project.mod_name}-${project.minecraft_version}"

task buildJar(type: Jar, group: "modding") {
    dependsOn ":common:build", ":fabric:build", ":neoforge:build"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from zipTree("common/build/libs/common-${project.version}.jar"),
        zipTree("fabric/build/libs/fabric-${project.version}.jar"),
        zipTree("neoforge/build/libs/neoforge-${project.version}.jar")

    from('./') {
        include 'LICENSE.md'
    }

    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.vendor,
                "Specification-Version"   : project.version,
                "Implementation-Title"    : project.mod_name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : project.vendor,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
assemble.finalizedBy("buildJar")
